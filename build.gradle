plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
	id "org.sonarqube" version "5.1.0.4882"
}

group = 'de.hsrm.mi.swt.projekt'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'com.google.code.gson:gson:2.11.0'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.joml:joml:1.10.8'
	implementation 'org.python:jython-standalone:2.7.3'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Frontend-Erstellung soll zusätzlich bei "./gradlew bootJar" ausgeführt werden
task build_frontend {
	doLast {
		// JS-Pakete in Vue-Projekt installieren
		exec {
			workingDir "${projectDir}/frontend"
			commandLine "npm install".split()
		}
		// Vue-Frontend bauen
		exec {
			workingDir "${projectDir}/frontend"
			commandLine "npm run build".split()
		}
		// eventuell vorhandenes public-Verzeichnis in Spring-Projekt löschen
		delete files("${projectDir}/src/main/resources/public")
		// dist-Ordner aus Vue-Projekt als Spring "public"-Ordner in src/main/resources kopieren
		copy {
			from "${projectDir}/frontend/dist"
			into "${projectDir}/src/main/resources/public"
		}
	}
}
bootJar.dependsOn build_frontend

task clean_frontend {
	doLast {
		delete files("${projectDir}/frontend/node_modules")
		delete files("${projectDir}/frontend/dist")
		delete files("${projectDir}/src/main/resources/public")
	}
}
clean.dependsOn clean_frontend

sonar {
	properties {
		property "sonar.token", "sqa_a1805cab0b66dc430efa030810147120b93b9871"
		property "sonar.organization", "SWT-Projekt Gruppe 01"
		property "sonar.host.url", "http://localhost:9000/sonar"
	}
}